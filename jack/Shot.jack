class Shot {
    field int x, y; // location of a Shots
    field boolean isActive;

    // Manejo de posiciones
    constructor Shot new() {
        let x = 0;
        let y = 0;
        return this;
    }

    method void setXY(int Ax, int Ay) {
        let x = Ax;
        let y = Ay;
        return;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method int getX() { return x; }
    method int getY() { return y; }
    method boolean getActive() { return isActive; }


    /** Dibujar disparo **/
        method void draw() {
        var int memAddress;
        let memAddress = 16384 + (x/16) + (y*32);
        // column 0
        do Memory.poke(memAddress, 3);
        do Memory.poke(memAddress +32, 3);
        do Memory.poke(memAddress +64, 3);
        do Memory.poke(memAddress +96, 3);
        do Memory.poke(memAddress +128, 3);
        return;
    }

    /** Borrar **/
    method void erase() {
        var int memAddress;
        let memAddress = 16384 + (x/16) + (y*32);
        // column 0
        do Memory.poke(memAddress, 0);
        do Memory.poke(memAddress +32, 0);
        do Memory.poke(memAddress +64, 0);
        do Memory.poke(memAddress +96, 0);
        do Memory.poke(memAddress +128, 0);
        return;
    }

    // Iniciar disparo
    method void fire(int Ax, int Ay) {
        do setXY(Ax, Ay);
        do activate();
        return;
    }

    method void run() {
        //Movimiento del disparo
        if (isActive) { do moveUp(); }
        return;
    }

    // Cambiar estaod del disparo a activo
    method void activate() {
        let isActive = true;
        do moveUp();
        return;
    }

    // Cambiar estado del disparo a inactivo (Desactivar)
    method void deactivate() {
        let isActive = false;
        do erase();
        return;
    }

    // Movimiento hacia arriba
    method void moveUp() {
        if (~isActive) { return; }
        if (y > 1) {
            do erase();
            let y = y - 3;
            do draw();
        }
        else {
            do deactivate();
        }
        return;
    }
}
